apply plugin: 'com.android.library'
apply plugin: 'com.jakewharton.hugo'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'com.github.dcendents.android-maven'

android {
    compileSdkVersion 23
    buildToolsVersion "22.0.1"

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 23
        multiDexEnabled true  /* required for building an apk [debug or release] */
        renderscriptTargetApi 23
        renderscriptSupportModeEnabled true

    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    lintOptions {
        abortOnError false
    }
}

ext {
    bintrayRepo = 'maven'
    bintrayName = 'backpack'

    publishedGroupId = 'com.github.dnldd'
    libraryName = 'backpack'
    artifact = 'library'

    libraryDescription = 'A collection of tools and patterns for android development'

    siteUrl = 'https://github.com/dnldd/backpack'
    gitUrl = 'https://github.com/dnldd/backpack.git'

    libraryVersion = '0.3.0'

    developerId = 'dnldd'
    developerName = 'Donald Adu-Poku'
    developerEmail = 'donald.adu@gmail.com'

    licenseName = 'The Apache Software License, Version 2.0'
    licenseUrl = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
    allLicenses = ["Apache-2.0"]
}

version = libraryVersion

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")

    configurations = ['archives']
    pkg {
        repo = bintrayRepo
        name = bintrayName
        desc = libraryDescription
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = allLicenses
        publish = true
        publicDownloadNumbers = true
        version {
            desc = libraryDescription
            gpg {
                sign = true //Determines whether to GPG sign the files. The default is false
                passphrase = properties.getProperty("bintray.gpg.password")
                //Optional. The passphrase for GPG signing'
            }
        }
    }
}

group = publishedGroupId   // Maven Group ID for the artifact

install {
    repositories.mavenInstaller {
        // This generates POM.xml with proper parameters
        pom {
            project {
                packaging 'aar'
                groupId publishedGroupId
                artifactId artifact

                // Add your description here
                name libraryName
                description libraryDescription
                url siteUrl

                // Set your license
                licenses {
                    license {
                        name licenseName
                        url licenseUrl
                    }
                }
                developers {
                    developer {
                        id developerId
                        name developerName
                        email developerEmail
                    }
                }
                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url siteUrl

                }
            }
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])

    compile ('com.android.support:appcompat-v7:23.0.0'){
        exclude module: 'support-v4'
        exclude module: 'support-annotations'
    }
    compile 'com.android.support:support-v4:23.0.0'
    compile 'com.android.support:support-annotations:23.0.0'
    compile ('com.android.support:gridlayout-v7:23.0.0'){
            exclude module: 'support-v4'
    }
    compile 'com.android.support:cardview-v7:23.0.0'
    compile ('com.android.support:recyclerview-v7:23.0.0'){
        exclude module: 'support-v4'
        exclude module: 'support-annotations'
    }
    compile ('com.android.support:palette-v7:23.0.0'){
        exclude module: 'support-v4'
    }
    compile ('com.android.support:design:23.0.0'){
        exclude module: 'appcompat-v7'
        exclude module: 'support-v4'
        exclude module: 'support-annotations'
    }

    compile 'com.squareup.picasso:picasso:2.5.2'
    compile 'com.squareup.retrofit:retrofit:1.9.0'
    compile ('com.squareup.retrofit:converter-simplexml:1.9.0') {
        exclude module: 'stax'
        exclude module: 'stax-api'
        exclude module: 'xpp3'
        exclude module: 'retrofit'
    }
    compile 'com.squareup.okhttp:okhttp:2.4.0'
    compile ('com.squareup.okhttp:okhttp-urlconnection:2.4.0'){
        exclude module: 'okhttp'
    }
    compile 'com.jakewharton:butterknife:7.0.1'
    compile ('com.google.android.gms:play-services:7.8.0'){
        exclude module: 'support-v4'
    }

    compile ('com.jakewharton.rxbinding:rxbinding-support-v4:0.2.0'){
        exclude module: 'support-v4'
        exclude module: 'rxbinding'
    }
    compile ('com.jakewharton.rxbinding:rxbinding-appcompat-v7:0.2.0'){
        exclude module: 'appcompat-v7'
        exclude module: 'rxbinding'
    }
    compile ('com.jakewharton.rxbinding:rxbinding-design:0.2.0'){
        exclude module: 'design'
        exclude module: 'rxbinding'
    }
    compile ('com.jakewharton.rxbinding:rxbinding-recyclerview-v7:0.2.0'){
        exclude module: 'recyclerview-v7'
        exclude module: 'rxbinding'
    }

    compile ('io.reactivex:rxandroid:1.0.1'){
        exclude module: 'rxandroid'
        exclude module: 'rxjava'
    }
    compile 'io.reactivex:rxjava:1.0.14'
    compile ('com.trello:rxlifecycle:0.3.0') {
        exclude module: 'rxandroid'
        exclude module: 'support-annotations'
        exclude module: 'rxbinding'
    }
    compile ('com.trello:rxlifecycle-components:0.3.0'){
        exclude module: 'rxlifecycle'
        exclude module: 'appcompat-v7'
    }

    compile 'commons-io:commons-io:2.4'
    compile 'com.jakewharton:disklrucache:2.0.2'
    compile 'org.jsoup:jsoup:1.8.3'
}
